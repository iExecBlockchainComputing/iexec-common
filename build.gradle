buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'com.iexec.common'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    deployerJars
}

ext {
    junitVersion = '4.12'
    web3jVersion = '4.1.1'
}

dependencies {
    testCompile "junit:junit:$junitVersion"

    // web3j dependency
    compile "org.web3j:core:$web3jVersion"

    // lombok
    compileOnly "org.projectlombok:lombok:1.18.2"
    annotationProcessor "org.projectlombok:lombok:1.18.2"

    // package cloud
    deployerJars "io.packagecloud.maven.wagon:maven-packagecloud-wagon:0.0.6"
}

test {
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

shadowJar { //required if we want to embed web3j dependency inside
    baseName = rootProject.name
    classifier = null
    version = version
}

build.dependsOn(shadowJar)

def gitBranch = 'git name-rev --name-only HEAD'.execute().text.trim()
def isMasterBranch = gitBranch == "master"
def canUploadArchives = isMasterBranch

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: "https://nexus.iex.ec/repository/hosted-maven/") {
            authentication(userName: nexusUser, password: nexusPassword)
        }
    }
}
uploadArchives.enabled = canUploadArchives
