plugins {
    id 'java-library'
    id 'io.freefair.lombok' version '8.2.2'
    id 'jacoco'
    id 'org.sonarqube' version '3.3'
    id 'maven-publish'
}

group = 'com.iexec.common'

ext {
    mockServerVersion = '5.11.2'
    openFeignVersion = '11.10'
}

if (!project.hasProperty('gitBranch')) {
    ext.gitBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
}

if (gitBranch != 'main' && gitBranch != 'master' && ! (gitBranch ==~ '(release|hotfix|support)/.*')) {
    version += '-NEXT-SNAPSHOT'
}

repositories {
    mavenCentral()
    maven {
        url = 'https://docker-regis-adm.iex.ec/repository/maven-public/'
        credentials {
            username nexusUser
            password nexusPassword
        }
    }
    maven {
        url "https://jitpack.io"
    }
}

// java-library plugin defines 'api' configuration
// 'api' configuration allows to expose dependencies with 'compile' scope in pom
// 'implementation' configuration allows to expose dependencies with 'runtime' scope in pom
dependencies {
    implementation platform('org.springframework.boot:spring-boot-dependencies:2.7.14')

    // feign
    api "io.github.openfeign:feign-jackson:$openFeignVersion"
    api "io.github.openfeign:feign-slf4j:$openFeignVersion"

    // iexec
    implementation "com.iexec.commons:iexec-commons-poco:$iexecCommonsPocoVersion"

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // apache commons.lang3
    implementation 'org.apache.commons:commons-lang3'

    //jaxb required with Java 11
    implementation 'javax.xml.bind:jaxb-api'

    //args utils
    implementation 'org.apache.maven.shared:maven-shared-utils:3.2.1'

    // zip
    implementation 'net.lingala.zip4j:zip4j:2.3.1'

    // google core libs
    implementation 'com.google.guava:guava:28.2-jre'

    implementation 'ch.qos.logback:logback-classic'

    implementation 'javax.annotation:javax.annotation-api'
    implementation 'javax.validation:validation-api'
    implementation 'org.hibernate.validator:hibernate-validator'
    implementation 'org.glassfish:javax.el:3.0.0'

    // expiring map
    implementation 'net.jodah:expiringmap:0.5.10'

    // test
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.assertj:assertj-core'
    // mock-server
    testImplementation "org.mock-server:mockserver-client-java:$mockServerVersion"
    testImplementation "org.mock-server:mockserver-junit-jupiter:$mockServerVersion"
    // spring-boot-test to capture test outputs
    testImplementation 'org.springframework.boot:spring-boot-test'
    // spring-test for ReflectionTestUtils
    testImplementation 'org.springframework:spring-test'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
    reports {
        junitXml.required = true
        html.required = true
    }
}

task itest {
    group 'Verification'
    description 'Runs the integration tests.'
}

jacoco {
    toolVersion = "0.8.7"
}
// sonarqube code coverage requires jacoco XML report
jacocoTestReport {
    reports {
        xml.required = true
    }
}
tasks.test.finalizedBy    tasks.jacocoTestReport
tasks.sonarqube.dependsOn tasks.jacocoTestReport

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username nexusUser
                password nexusPassword
            }
            url = project.hasProperty('nexusUrl') ? nexusUrl : ''
        }
    }
}
